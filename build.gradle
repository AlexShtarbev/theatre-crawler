buildscript {
    configurations['classpath'].resolutionStrategy.eachDependency {
        if (requested.group == 'org.jooq') {
            useVersion '3.18.7'
        }
    }
}

plugins {
    id 'java'
    id 'io.quarkus'
    // https://github.com/etiennestuder/gradle-jooq-plugin/blob/main/README.md
    id 'nu.studer.jooq' version '8.2.1'
    // https://contribute.liquibase.com/extensions-integrations/directory/integration-docs/gradle/
    id 'org.liquibase.gradle' version '2.2.1'
    // https://github.com/avast/gradle-docker-compose-plugin
    id "com.avast.gradle.docker-compose" version "0.17.5"
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-jdbc-postgresql'
    implementation 'io.quarkus:quarkus-agroal'
    implementation 'io.quarkiverse.jooq:quarkus-jooq:2.0.0'
    implementation 'io.quarkus:quarkus-jackson'
    implementation 'io.quarkus:quarkus-arc'
    // https://mvnrepository.com/artifact/org.jooq/jooq
    implementation 'org.jooq:jooq:3.18.7'
    implementation 'org.immutables:value:2.9.2'
    annotationProcessor 'org.immutables:value:2.9.2'
    compileOnly 'org.immutables:value:2.9.2'
    // https://mvnrepository.com/artifact/com.microsoft.playwright/playwright
    implementation 'com.microsoft.playwright:playwright:1.40.0'
    // https://mvnrepository.com/artifact/org.jboss.slf4j/slf4j-jboss-logmanager
    implementation 'org.jboss.slf4j:slf4j-jboss-logmanager:2.0.1.Final'
    implementation "com.google.guava:guava:32.1.3-jre"

    testImplementation 'io.quarkus:quarkus-junit5'

    liquibaseRuntime 'org.liquibase:liquibase-core:4.16.1'
    liquibaseRuntime 'info.picocli:picocli:4.6.1'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.2'
    liquibaseRuntime "org.postgresql:postgresql:42.5.1"

    jooqGenerator 'org.postgresql:postgresql:42.5.1'
}

group 'org.ays.theatre.crawler'
version '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

apply from: 'liquibase.gradle'
apply from: 'jooq.gradle'

dockerCompose {
    useComposeFiles = ['src/main/docker/infra/postgres.yaml']
    isRequiredBy(generateJooq)
}

update.dependsOn(composeUp)
generateJooq.dependsOn(update)

